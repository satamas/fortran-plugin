// general part
buildscript {
    ext.javaVersion = '1.8'

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }

    }
    dependencies {
        classpath "gradle.plugin.org.jetbrains:gradle-intellij-plugin:0.1.10"
    }
}

plugins {
    id 'org.jetbrains.intellij' version "0.2.14"
    id 'org.jetbrains.kotlin.jvm' version '1.1.3-2'
    id 'de.undercouch.download' version '3.2.0'
}

allprojects {
    apply plugin: 'idea'
    idea {
        module {
            generatedSourceDirs += file('src/gen')
        }
    }

    apply plugin: 'org.jetbrains.intellij'
    intellij {
        version ideaVersion
        downloadSources Boolean.valueOf(downloadIdeaSources)
        updateSinceUntilBuild = false
        instrumentCode = false
        alternativeIdePath = "debugger/lib/clion"
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    apply plugin: 'java'
    apply plugin: 'kotlin'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
    sourceSets {
        main {
            java.srcDirs += 'src/gen'
        }
    }
    dependencies {
        compileOnly('com.github.JetBrains:Grammar-Kit:1.5.2') {
            exclude group: 'org.jetbrains.plugins'
            exclude module: 'idea'
        }

        testCompile 'junit:junit:4.+'
    }
}

test {
    useJUnit ()
}

lexerTask(project, 'FortranLexer', 'org/jetbrains/fortran/lang/lexer')
parserTask(project, 'FortranParser', 'org/jetbrains/fortran/lang/parser')

static def codegenTask(project, task) {
    project.compileKotlin.dependsOn task
    project.compileJava.dependsOn task
    project.compileTestJava.dependsOn task
    return task
}

def lexerTask(project, lexerName, pkg) {
    return codegenTask(project, tasks.create("generate${lexerName}", JavaExec) {
        def src = "$project.projectDir/src/main/kotlin/org/jetbrains/fortran/lang/lexer/${lexerName}.flex"
        def dst = "$project.projectDir/src/gen/$pkg"

        main = 'jflex.Main'
        classpath = files('lib/jflex/JFlex.jar')

        args = ['--skel', 'lib/jflex/idea-flex.skeleton',
                '-d', dst,
                src
        ]

        inputs.file file(src)
        outputs.dir file("$dst/_${lexerName}.java")
    })
}

def parserTask(project, parserName, pkg) {
    return codegenTask(project, tasks.create("generate${parserName}", JavaExec) {
        def dstRoot = "$project.projectDir/src/gen/"
        def src = "$project.projectDir/src/main/kotlin/org/jetbrains/fortran/lang/parser/${parserName}.bnf"
        def dst = "$dstRoot/$pkg"
        doFirst {
            delete file("$dstRoot/org/jetbrains/fortran/lang/parser")
            delete file("$dstRoot/org/jetbrains/fortran/lang/psi")
            delete file("$dstRoot/org/jetbrains/fortran/lang/FortranTypes.java")
        }

        main = 'org.intellij.grammar.Main'
        classpath(configurations.compileOnly)

        args = [dstRoot, file(src)]

        inputs.file file(src)
        outputs.dir dst
    })
}

project(':') {
    sourceSets {
        main {
            kotlin.srcDirs += 'debugger/src/main/kotlin'
            compileClasspath += files("debugger/lib/clion/lib/clion.jar")
        }
    }
}

def isWindows = System.getProperty("os.name").toLowerCase().indexOf( "win" ) >= 0

task downloadClion(type: de.undercouch.gradle.tasks.download.Download) {
    if (isWindows) {
        src "https://download.jetbrains.com/cpp/CLion-${clionVersion}.zip"
        dest new File(project.projectDir, "debugger/lib/clion-${clionVersion}.zip")
    } else {
        src "https://download.jetbrains.com/cpp/CLion-${clionVersion}.tar.gz"
        dest new File(project.projectDir, "debugger/lib/clion-${clionVersion}.tar.gz")
    }

    overwrite false
}

task unpackClion(dependsOn: downloadClion, type: Copy) {
    if (isWindows) {
        from zipTree("debugger/lib/clion-${clionVersion}.zip")
        into new File(project.projectDir, "debugger/lib/clion")
    } else {
        from tarTree(resources.gzip("debugger/lib/clion-${clionVersion}.tar.gz"))
        into new File(project.projectDir, "debugger/lib/")

        doLast {
            file("debugger/lib/clion-${clionVersion}").renameTo(file("debugger/lib/clion"))
        }
    }

}

compileKotlin.dependsOn unpackClion
